wrapper {
  gradleVersion = "2.4"
}

enum Platform {
  OsX('mac os x|darwin|osx'),
  Windows('windows'),
  Linux('linux')
  Platform(String regex) { this.regex = regex }
  String regex
  static Platform current() {
    def osName = System.getProperty('os.name')
     values().find { osName =~ "(?i)${it.regex}" }
   }
}

ext.platform = Platform.current()

apply plugin: 'cpp'
apply plugin: 'c'

task lex(type: LexCompiler) {
  sourceDir = project.file("${buildDir}/src/generated/lex")
  headerDir = project.file("${buildDir}/src/generated/lex")
  lexFiles = files('src/chuck.lex')
}

task yacc(type: YaccCompiler) {
  sourceDir = project.file("${buildDir}/src/generated/yacc")
  headerDir = project.file("${buildDir}/src/generated/yacc")
  yaccFiles = files('src/chuck.y')
}

binaries.all {
  // Define toolchain-specific compiler and linker options
  if (project.platform == Platform.OsX) {
    // TODO: Determine automatically
    cCompiler.args '-DHAVE_CONFIG_H', '-D__MACOSX_CORE__', '-isysroot',
      '/Applications/Xcode.app/Contents/Developer/Platforms/MacOSX.platform/Developer/SDKs/MacOSX10.10.sdk', \
      '-mmacosx-version-min=10.4'
    cppCompiler.args '-D__MACOSX_CORE__', '-isysroot', \
      '/Applications/Xcode.app/Contents/Developer/Platforms/MacOSX.platform/Developer/SDKs/MacOSX10.10.sdk', \
      '-mmacosx-version-min=10.4'
    linker.args '-F/System/Library/PrivateFrameworks', '-weak_framework', 'MultitouchSupport', \
      '-framework', 'CoreAudio', '-framework', 'CoreMIDI', '-framework', 'CoreFoundation', '-framework', \
      'IOKit', '-framework', 'Carbon', '-framework', 'AppKit', '-framework', 'Foundation', \
      '-mmacosx-version-min=10.4'
  } else if (project.platform == Platform.Linux) {
    cCompiler.args '-DHAVE_CONFIG_H', '-D__LINUX_ALSA__', '-D__PLATFORM_LINUX__'
    cppCompiler.args '-D__LINUX_ALSA__', '-D__PLATFORM_LINUX__'
    linker.args '-lasound', '-lstdc++', '-ldl', '-lm', '-lsndfile', '-lpthread'
  }
}

class LexCompiler extends DefaultTask {
  @InputFiles FileCollection lexFiles
  @OutputDirectory File sourceDir
  @OutputDirectory File headerDir

  @TaskAction
  void processLexFiles() {
    lexFiles.each { file ->
      project.exec {
        commandLine = ['flex', '-o', new File(sourceDir, file.name - '.lex' + '.yy.c').absolutePath,
          file.absolutePath]
      }
    }
  }
}

class YaccCompiler extends DefaultTask {
  @InputFiles FileCollection yaccFiles
  @OutputDirectory File sourceDir
  @OutputDirectory File headerDir

  @TaskAction
  void processYaccFiles() {
    yaccFiles.each { file ->
      project.exec {
        commandLine = ['bison', '-dv', '-o', new File(sourceDir, file.name - '.y' + '.tab.c'),
          file.absolutePath]
      }
    }
  }
}

model {
  components {
    chuck(NativeExecutableSpec) {
      sources.all {
        exportedHeaders {
          srcDir 'src'
          srcDir 'src/lo'
        }
      }
      sources {
        yaccOutput(CSourceSet) {
          generatedBy tasks.yacc
          source.include '*.c'
        }
        lexOutput(CSourceSet) {
          generatedBy tasks.lex
          source.include '*.c'
          lib sources.yaccOutput
        }
        c {
          source {
            srcDir 'src'
            include '*.c'
            include 'lo/*.c'
            if (project.platform != Platform.Windows) {
              exclude 'chuck_win32.c'
            }
            if (project.platform != Platform.OsX) {
              exclude 'util_sndfile.c'
            }
          }
          lib sources.lexOutput
          lib sources.yaccOutput
        }
        cpp {
          source {
            srcDir 'src'
            include '*.cpp'
            include 'RtAudio/*.cpp'
            exclude 'rtaudio_c.cpp'
            exclude 'chuck_js.cpp'
            exclude 'digiio_webaudio.cpp'
          }
        }
      }
    }
  }
}

if (System.getenv("EMSCRIPTEN")?.trim()) {
  logger.info("Emscripten detected - Configuring targets")
  apply plugin: 'emscripten'
} else {
  logger.info("Emscripten not detected - Not configuring targets")
}
